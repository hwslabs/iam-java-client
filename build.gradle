import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'org.openapi.generator'

group = 'com.hypto'

targetCompatibility = 17

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo1.maven.org/maven2" }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:6.0.1"
        classpath 'com.android.tools.build:gradle:2.3.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
    }
}

wrapper {
    gradleVersion = "8.5"
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

repositories {
    mavenCentral()
}
sourceSets {
    main.java.srcDirs = ['src/main/java']
}

if(hasProperty('target') && target == 'android') {

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'

    android {
        compileSdkVersion 25
        buildToolsVersion '25.0.2'
        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 25
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        // Rename the aar correctly
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.aar')) {
                    def fileName = "${project.name}-${variant.baseName}-${version}.aar"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }

        dependencies {
            provided "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"
        }
    }

    afterEvaluate {
        android.libraryVariants.all { variant ->
            def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
            task.description = "Create jar artifact for ${variant.name}"
            task.dependsOn variant.javaCompile
            task.from variant.javaCompile.destinationDir
            task.destinationDir = project.file("${project.buildDir}/outputs/jar")
            task.archiveName = "${project.name}-${variant.baseName}-${version}.jar"
            artifacts.add('archives', task);
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    artifacts {
        archives sourcesJar
    }

} else {

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = 'iam-java-client'
                from components.java
            }
        }
    }

    task execute(type:JavaExec) {
        main = System.getProperty('mainClass')
        classpath = sourceSets.main.runtimeClasspath
    }
}

ext {
    oltu_version = "1.0.1"
    retrofit_version = "2.9.0"
    swagger_annotations_version = "1.6.6"
    junit_version = "5.9.2.2"
    json_fire_version = "1.8.5"
    jakarta_annotation_version = "1.3.5"
    casbin_version = '1.28.0'
}

dependencies {
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation ("org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$oltu_version"){
        exclude group:'org.apache.oltu.oauth2' , module: 'org.apache.oltu.oauth2.common'
    }
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "io.gsonfire:gson-fire:$json_fire_version"
    implementation 'javax.ws.rs:jsr311-api:1.1.1'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation "jakarta.annotation:jakarta.annotation-api:$jakarta_annotation_version"

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
    //    runtimeOnly 'org.bouncycastle:bcprov-jdk15on:1.60'
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.11.5' // or 'io.jsonwebtoken:jjwt-jackson:0.11.2' for gson

    // Casbin
    implementation("org.casbin:jcasbin:$casbin_version") // Policy management


    testImplementation "junit:junit:$junit_version"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    testImplementation 'org.mockito:mockito-core:4.7.0'
}

javadoc {
    options.tags = [ "http.response.details:a:Http Response Details" ]
}

// Use spotless plugin to automatically format code, remove unused import, etc
// To apply changes directly to the file, run `gradlew spotlessApply`
// Ref: https://github.com/diffplug/spotless/tree/main/plugin-gradle
spotless {
    // comment out below to run spotless as part of the `check` task
//    enforceCheck false

    format 'misc', {
        // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
        target '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        // don't need to set target, it is inferred from java

        // apply a specific flavor of google-java-format
        googleJavaFormat('1.14.0').aosp().reflowLongStrings()

        removeUnusedImports()
        importOrder()
    }
}
tasks.named("spotlessJava").configure { dependsOn("generateClient") }
tasks.named("spotlessMisc").configure { dependsOn("generateClient") }

task deleteFiles {
    // Adding doLast to the task as it should only run until called explicitly
    doLast {
        new File('.openapi-generator/FILES').eachLine {
            def file = new File("$rootDir/$it")
            if (file.exists()) {
                file.delete()
            }
        }
    }
}

task generateClient(dependsOn: [deleteFiles], type: GenerateTask) {
    generatorName = "java"
    inputSpec = "$rootDir/iam_openapi_spec.yml".toString()
    outputDir = "$rootDir".toString()
    templateDir = "$rootDir/templates"
    groupId = "com.hypto"
    id = "iam-java-client"
    invokerPackage = "com.hypto.iam.client"
    modelPackage = "com.hypto.iam.client.model"
    apiPackage = "com.hypto.iam.client.api"
    generateModelTests = false
    generateApiTests = false
    configOptions = [
            hideGenerationTimestamp : "true",
            dateLibrary: "java8",

            artifactDescription: "IAM Java Client",
            artifactUrl: "https://github.com/hwslabs/iam-java-client",
            scmConnection: "scm:git:git@github.com:hwslabs/iam-java-client.git",
            scmDeveloperConnection: "scm:git:git@github.com:hwslabs/iam-java-client.git",
            scmUrl: "https://github.com/hwslabs/iam-java-client.git"
    ]
    additionalProperties = [
            library: "retrofit2"
    ]

    finalizedBy(spotlessApply)
}
